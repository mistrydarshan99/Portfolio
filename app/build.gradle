apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'io.fabric'
apply from: "../utils.gradle"


android {
    compileSdkVersion versions.compile_sdk
    defaultConfig {
        applicationId versions.app_id
        minSdkVersion versions.min_sdk
        targetSdkVersion versions.target_sdk
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        versionCode getVersionCode(versions.major, versions.minor, versions.patch, versions.build)
        versionName getVersionName(versions.major, versions.minor, versions.patch)
        testInstrumentationRunner deps.junit_runner
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOGS", "true"
            buildConfigField "boolean", "DEV_ENVIRONMENT", "true"

            versionNameSuffix "-debug"
        }

        release {
            buildConfigField "boolean", "LOGS", "false"
            buildConfigField "boolean", "DEV_ENVIRONMENT", "false"

            zipAlignEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    androidExtensions {
        experimental = true
    }

    applicationVariants.all {
        variant ->
            if (variant.buildType.name == "release") {
                renameArtifact(versions.app_name, variant)
            }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Foundation
    implementation deps.kotlin_stdlib                   // Kotlin
    implementation deps.androidx_core                   // AndroidX core
    implementation deps.androidx_futures                // AndroidX futures
    implementation deps.androidx_appcompat              // Appcompat
    implementation deps.androidx_emojicompat            // Emoji
    implementation deps.androidx_vector                 // Vector drawable
    implementation deps.android_material                // Material design
    implementation deps.androidx_constraint             // Constraint layout
    implementation deps.androidx_pager                  // Viewpager

    // Architecture components
    implementation deps.androidx_lifecycles             // Lifecycles - ViewModel and LiveData
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    kapt deps.lifecycles_compiler                       // Lifecycles annotation
    implementation deps.androidx_room                   // Room
    kapt deps.room_compiler                             // Room annotation
    implementation deps.room_coroutines                 // Room coroutines
    implementation deps.androidx_paging                 // Paging
    implementation deps.androidx_nav                    // Navigation controller
    implementation deps.nav_ui                          // Navigation controller UI
    implementation deps.androidx_work                   // Workmanager

    //Firebase products
    implementation deps.fb_core                         // Core
    implementation deps.fb_crash                        // Crashlytics
    implementation deps.fb_config                       // Remote config
    implementation deps.fb_index                        // App index
    implementation deps.fb_amsg                         // In app message

    // Network
    implementation deps.retrofit2                       // Retrofit2
    implementation deps.retrofit2_gson                  // Retrofit2 Gson converter
    implementation deps.retrofit2_coroutines            // Retrofit2 Coroutines adapter
    implementation deps.okhttp3                         // OkHttp3
    implementation deps.okhttp3_interceptor             // OkHttp3 logging interceptor
    implementation deps.glide                           // Glide
    kapt deps.glide_annotation           // Glide Annotation
    implementation deps.koin                            // Koin dependency injection
    implementation deps.koin_viewmodel                  // Koin AndroidX viewmodel
    implementation deps.koin_scope                      // Koin Android scope
    implementation deps.coroutines                      // Kotlin coroutines
    implementation deps.coroutines_android              // Kotlin Android coroutines
    implementation deps.stetho                          // Stetho
    implementation deps.stetho_okhttp3                  // Stetho OkHttp3

    // Utils
    implementation deps.shimmer                         // Facebook's shimmer layout
    implementation deps.timber                          // Timber logging
    implementation deps.three10                         // ThreeTenABP
    debugImplementation deps.leak_canary                // Leak Canary
    releaseImplementation deps.leak_canary_no_op        // Leak Canary no operation
    debugImplementation deps.leak_canary_fragment       // Leak Canary fragment
    implementation deps.sdp                             // Scalable dependency pixel
    implementation deps.circle                          // Circle image view
    implementation deps.guava                           // Guava

    // Test
    testImplementation deps.junit                       // JUnit test
    androidTestImplementation deps.androidx_test        // AndroidX test
    androidTestImplementation deps.androidx_espresso    // Espresso test
    testImplementation deps.androidx_arch_test          // AndroidX architecture component test
    testImplementation deps.androidx_paging_test        // AndroidX Paging test
    testImplementation deps.androidx_room_test          // AndroidX Room test
}
apply plugin: 'com.google.gms.google-services'
