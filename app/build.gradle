apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'io.fabric'
apply from: "../utils.gradle"


android {
    compileSdkVersion build_versions.compile_sdk
    defaultConfig {
        applicationId build_versions.app_id
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        versionCode getVersionCode(build_variants.major, build_variants.minor, build_variants.patch, build_variants.build)
        versionName getVersionName(build_variants.major, build_variants.minor, build_variants.patch)
        testInstrumentationRunner deps.androidx.test_runner
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOGS", "true"
            buildConfigField "boolean", "DEV_ENVIRONMENT", "true"

            versionNameSuffix "-debug"
        }

        release {
            buildConfigField "boolean", "LOGS", "false"
            buildConfigField "boolean", "DEV_ENVIRONMENT", "false"

            zipAlignEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    androidExtensions {
        experimental = true
    }

    applicationVariants.all {
        variant ->
            if (variant.buildType.name == "release") {
                renameArtifact(build_versions.app_name, variant)
            }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation deps.kotlin.stdlib

    // Google
    implementation deps.google.material
    implementation deps.google.guava

    // AndroidX Foundation
    implementation deps.androidx.core
    implementation deps.androidx.legacy
    implementation deps.androidx.futures
    implementation deps.androidx.appcompat
    implementation deps.androidx.vector
    implementation deps.androidx.constraint
    implementation deps.androidx.viewpager
    implementation deps.androidx.emoji

    // Android Architecture components
    implementation deps.lifecycle.viewmodel
    implementation deps.lifecycle.extension
    kapt deps.lifecycle.compiler
    implementation deps.room.runtime
    implementation deps.room.ktx
    kapt deps.room.compiler
    implementation deps.navigation.fragment
    implementation deps.navigation.ui
    implementation deps.paging.runtime
    implementation deps.work.runtime

    // Firebase components
    implementation deps.firebase.core
    implementation deps.firebase.crashlytics
    implementation deps.firebase.config
    implementation deps.firebase.appindex

    // Dependency injection
    implementation deps.koin.viewmodel

    // Network components
    implementation deps.coroutines.core
    implementation deps.coroutines.android
    implementation deps.retrofit.core
    implementation deps.retrofit.moshi
    implementation deps.retrofit.coroutines
    implementation deps.moshi.core
    implementation deps.moshi.kotlin
    kapt deps.moshi.codegen
    implementation deps.moshi.adapters
    implementation deps.okhttp.core
    implementation deps.okhttp.logging
    implementation deps.glide.core
    kapt deps.glide.compiler
    implementation deps.stetho.core
    implementation deps.stetho.okhttp

    // Utilities
    debugImplementation deps.leak_canary.core
    releaseImplementation deps.leak_canary.no_op
    debugImplementation deps.leak_canary.fragment
    implementation deps.utilities.shimmer
    implementation deps.utilities.timber
    implementation deps.utilities.three_ten_abp
    implementation deps.utilities.sdp
    implementation deps.utilities.circle_image_view

    // Test
    testImplementation deps.test.junit
    androidTestImplementation deps.androidx.test
    androidTestImplementation deps.androidx.espresso
    testImplementation deps.room.test
    testImplementation deps.androidx.arch_test
    testImplementation deps.paging.test
    testImplementation deps.koin.test
    androidTestImplementation deps.work.test
}
apply plugin: 'com.google.gms.google-services'
